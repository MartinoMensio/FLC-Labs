import java_cup.runtime.*;
import java.io.*;
import java.util.*;



parser code {:

    public boolean isCorrect = true;

    // Redefinition of error functions
    /* The report_error function, in this program, is called only when
       an error, not managed by the error symbol, is found.
       Indeed, when errors are recognized by the error symbol, the function
       syntax_error (disabled in this program) is called.
       This program is an example of error function redefinition: two new
       functions are developed, pSynError and pSynWarning, used to print
       syntactical errors and warning, respectively. */
    public void report_error(String message, Object info) {
        System.err.print("ERROR: Syntax error");
        if (info instanceof Symbol)
            if (((Symbol)info).left != -1){
                int line = (((Symbol)info).left)+1;
                int column = (((Symbol)info).right)+1;
                System.err.print(" (linea "+line+", colonna "+column+"): ");
            } else System.err.print(": ");
        else System.err.print(": ");
    }
    public void syntax_error(Symbol cur_token){}

        // Return the line number of actual symbol
    public int getLine() {
        if (((Symbol)stack.elementAt(tos)).left != -1){
            return ((Symbol)stack.elementAt(tos)).left+1;
        }else return -1;
    }
    // Return the column number of actual symbol
    public int getColumn() {
        if (((Symbol)stack.elementAt(tos)).left != -1){
            return ((Symbol)stack.elementAt(tos)).right+1;
        }else return -1;
    }

    public Object stack(int position) throws Exception {
        if(position > 0) {
            throw new Exception("Trying to access stack of the future!");
        }
        return (((Symbol)stack.elementAt(tos+position)).value);
    }
    
    public int genLabel() {
        return ++labelCount;
    }
    private int labelCount;
    
    public Map<String, String> variables;
    
:};


init with {:
    variables = new HashMap<>();
    labelCount = 0;
:};


action code {:

    private void pSynError(String message){
        System.err.println("SYN ERROR: line: "+parser.getLine()+" col: "+parser.getColumn()+": "+message);
        parser.isCorrect = false;
        parser.done_parsing();
    }
    private void pSynWarning(String message){
        System.err.println("SYN WARNING: line: "+parser.getLine()+" col: "+parser.getColumn()+": "+message);
        parser.isCorrect = false;
    }
        
:}


// Terminal tokens
terminal INT, DOUBLE;

terminal PRINT, IF, WHILE, ELSE;
terminal ID;
terminal RO, RC, BO, BC, S, CM, SO, SC;
terminal PLUS, MINUS, STAR, DIV;
terminal MIN, MAJ, MIN_EQ, EQ_MIN, MAJ_EQ, EQ_MAJ, EQ;
terminal AND, OR, NOT;
terminal String INT_TYPE, DOUBLE_TYPE;
terminal UMINUS;


// Non terminal tokens
non terminal prog, stmt_list, stmt, if, while, assignment, print;
non terminal String exp;
non terminal mineq, majeq;

non terminal decl_list, decl, var_list, var;
non terminal String type;
non terminal array;
non terminal if_condition, while_condition;
non terminal String id;

non terminal Integer[] NT0, NT1, NT2;

// Precedences and associativities
// lower precedences
precedence left OR;
precedence left AND;
precedence left NOT;
precedence left MIN, MAJ, MIN_EQ, EQ_MIN, MAJ_EQ, EQ_MAJ, EQ;
precedence left PLUS, MINUS;
precedence left STAR, DIV;
precedence left UMINUS;
precedence nonassoc IF;
// higher precedences


//////////////////////////////////////
// Grammar start
//////////////////////////////////////

start with prog;


prog ::= decl_list stmt_list {: if (parser.isCorrect) System.err.println("Program correctly recognized"); :}
;


//////////////////////////////////////
// Declarations
//////////////////////////////////////

decl_list ::= decl_list decl |
;

decl ::= type var_list S
    | type error S {: pSynWarning("Error in declaration"); :}
;

type ::= INT_TYPE {: RESULT = "INT"; :}
    | DOUBLE_TYPE {: RESULT = "DOUBLE"; :}
;

var_list ::= var:a {: String type = (String)parser.stack(-1); parser.variables.put((String)a, type); System.out.println(type + " " + a); RESULT = type; :}
    | var_list:a CM var:b {: parser.variables.put((String)a, (String)b); System.out.println(a + " " + b); RESULT = b; :}
;

var ::= ID:a {: RESULT = a; :}
    | ID:a SO INT:b SC  {: RESULT = (String)a + "[" + b + "]" ; :}
;


//////////////////////////////////////
// Instructions
//////////////////////////////////////

stmt_list ::= stmt_list stmt | stmt 
            | error stmt {: pSynWarning("Error in statement"); :}
;


stmt ::= if | while | assignment | print | BO stmt_list BC
    | BO stmt_list error BC {: pSynWarning("Missing ; before }"); :}
    | BO error BC {: pSynWarning("Missing ; before }"); :}
    | error S {: pSynWarning("Error in statement"); :}
;   

// Assignment instruction
assignment ::= id:a S {: System.out.println("EVAL " + a); :}
    | id:a EQ exp:b S {: System.out.println("EVAL " + b + "\n" + "ASS " + a); :}
    | id EQ error S {: pSynWarning("Error in expression"); :}
    | error EQ exp S {: pSynWarning("Error in assigment"); :}
;


// PRINT instruction
print ::= PRINT id S
          | PRINT error S {: pSynWarning("Error in 'print' instruction"); :}
;


// IF instruction
if ::= IF if_condition NT1:a stmt ELSE NT2:b stmt
     | IF if_condition NT1:a stmt
     | IF if_condition NT1:a stmt error stmt {: pSynWarning("Error 'else' expected in 'if' instruction"); :}
;

NT1 ::= {: 
    RESULT = new Integer[2];
    RESULT[0] = parser.genLabel(); //L0:
    RESULT[1] = parser.genLabel(); //L1:
    System.out.println("EVAL"+parser.stack(0));
    System.out.println("GOTOF L"+RESULT[0]);
:};

NT2 ::= {:
    // TODO
:};

if_condition ::= RO exp:a RC {: System.out.println("EVAL " + a); :}
    | RO error RC {: pSynWarning("Error in 'if' condition"); :}
    | error exp RC {: pSynWarning("Error '(' expected in 'if' instruciton"); :}
    | RO exp error  {: pSynWarning("Error ')' expected in 'if' instruciton"); :}
;


// WHILE instruction
while ::= WHILE while_condition NT0:a stmt {: Integer[] l = a;
    System.out.println("GOTO L" +l[0]);
    System.out.print("L" + l[1] + ":");
    :}
;

NT0 ::= {:
    RESULT = new Integer[2];
    RESULT[0] = parser.genLabel(); //L0:
    RESULT[1] = parser.genLabel(); //L1:
    System.out.print("L"+RESULT[0]+":");
    System.out.println("EVAL"+parser.stack(0));
    System.out.println("GOTOF L"+RESULT[1]);
:};

while_condition ::= RO exp:a RC {: RESULT = a; :}
    | RO error RC {: pSynWarning("Error in 'while' condition"); :}
    | error exp RC {: pSynWarning("Error '(' expected in 'while' instruciton"); :}
    | RO exp error  {: pSynWarning("Error ')' expected in 'while' instruciton"); :}
;


// Expressions
exp ::= 
    /* Logical expressions */
    exp:a AND exp:b {: RESULT = a + " " + b + " &"; :}
    | exp:a OR exp:b {: RESULT = a + " " + b + " |"; :}
    | NOT exp:a {: RESULT = a + " !"; :}

    /* Comparison expressions */
    | exp:a EQ EQ exp:b {: RESULT = a + " " + b + " =="; :}
    | exp:a MIN exp:b {: RESULT = a + " " + b + " <"; :}
    | exp:a MAJ exp:b {: RESULT = a + " " + b + " >"; :}
    | exp:a mineq exp:b {: RESULT = a + " " + b + " <="; :}
    | exp:a majeq exp:b {: RESULT = a + " " + b + " >)"; :}

    /* Arithmetic expression */
    | exp:a PLUS exp:b {: RESULT = a + " " + b + " +"; :}
    | exp:a MINUS exp:b {: RESULT = a + " " + b + " -"; :}
    | exp:a STAR exp:b {: RESULT = a + " " + b + " *"; :}
    | exp:a DIV exp:b {: RESULT = a + " " + b + " /"; :}
    | RO exp:a RC {: RESULT = a; :}
    | id:a {: RESULT = a; :}
    | INT:a {: RESULT = Integer.toString((Integer)a); :}
    | DOUBLE:a {: RESULT = Double.toString((Double)a); :}
    | MINUS INT:a {: RESULT = "-" + a; :}
    | MINUS DOUBLE:a {: RESULT = "-" + a; :}
    | RO error RC {: pSynWarning("Error in expression"); RESULT = ""; :}
;


mineq ::= MIN_EQ | EQ_MIN;
majeq ::= MAJ_EQ | EQ_MAJ;


id ::= ID:a {: RESULT = (String)a; :}
      |ID:a SO INT:b SC {: RESULT = a + "[" + b + "]"; :}
      |ID:a SO ID:b SC {: RESULT = a + "[" + b + "]"; :}
      | error SC {: pSynWarning("Error in vector"); :}
;
